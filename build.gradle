buildscript {
    repositories {
		mavenCentral()
        jcenter()
        maven {
			url = "http://files.minecraftforge.net/maven"
		}
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.

version = "${version}"
group = "com.subatomicnetworks.subatomicscience" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "subatomicscience"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
	runDir = "run"
	mappings = "${mappings_version}"
	version = "${mcversion}-${forge_version}"
	replaceIn "${mod_info_path}"
	replace "%VERSION%", "${version}"
}

version = "${mcversion}-${mod_version}-${release_version}"

repositories {
	maven { // The repo from which to get waila
		name "Mobius Repo"
		// url "http://mobiusstrip.eu/maven"
		url "http://tehnut.info/maven"
	}

	maven { // TheOneProbe
		name 'tterrag maven'
		url "http://maven.tterrag.com/"
	}
}

dependencies {
	deobfCompile "mcp.mobius.waila:Hwyla:${waila_version}:api"

	deobfCompile("mcjty.theoneprobe:TheOneProbe-${the_one_probe_version}") {
		transitive = false
	}
}

sourceSets {
	main {
		java {
			srcDir 'src/main/java'
		}
		resources {
			srcDir 'src/main/resources'
		}
	}
	api {
		java {
			srcDir 'src/api/java'
		}
	}
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
	
	// Put Access Transformer in the META-INF folder
	rename '(.+_at.cfg)', 'META-INF/$1'
}

// Access Transformer
jar {
	manifest {
	    attributes 'FMLAT': 'subatomicscience_at.cfg'
	}
}

// -----------
// extra jars (based on buildcraft 6.1.x's build.gradle)
// https://github.com/BuildCraft/BuildCraft/blob/6.1.x/build.gradle
// -----------

// source jar
task sourceJar_new(type: Jar) {
	from sourceSets.main.allSource
	from sourceSets.api.allSource
	classifier = 'sources'
}

// add api to javadoc
javadoc {
	source += sourceSets.api.allSource
}

// javadoc jar
task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from 'build/docs/javadoc'
}

// api jar
task apiJar(type: Jar) {
	from sourceSets.api.output
	classifier = 'api'
}

jar {
	from sourceSets.api.output
}

build.dependsOn sourceJar_new, apiJar